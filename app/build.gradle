plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

apply from: "../dependencies.gradle"

android {
    compileSdk "$libraryVersions.compileSdk".toInteger()

    signingConfigs {
        releaseConfig {
            try {
                keyAlias 'keydata.keystore'
                keyPassword KEY_PASSWORD
                storeFile file('../misc/keydata.keystore.jks')
                storePassword STORE_PASSWORD
            } catch (Exception ex) {
                throw new Exception(ex.getMessage())
            }
        }
    }

    namespace 'com.meembusoft.ln'

    defaultConfig {
        applicationId "com.meembusoft.ln"
        minSdk "$libraryVersions.minSdk".toInteger()
        targetSdk "$libraryVersions.targetSdk".toInteger()

        vectorDrawables.useSupportLibrary = true

        // define your base Applications name here
        resValue 'string', 'app_name', 'Lagbe Naki?'
    }

    buildTypes {
        debug {
            shrinkResources false
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            shrinkResources true
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    //Flavor definition
    flavorDimensions "appType", "clientType"
    productFlavors {
        // App types
        user {
            dimension "appType"

            applicationIdSuffix ".user"
        }
        admin {
            dimension "appType"

            applicationIdSuffix ".admin"
        }
        driver {
            dimension "appType"

            applicationIdSuffix ".driver"
        }

        // Client types
        bd {
            dimension "clientType"

            applicationIdSuffix ".bd"
            versionCode "$libraryVersions.userBDVersionCode".toInteger()
            versionName "$libraryVersions.userBDVersionName"

            resValue 'string', 'app_name', 'Lagbe Naki?'
        }
        rashedshop {
            dimension "clientType"

            applicationIdSuffix ".rashedshop"
            versionCode "$libraryVersions.rashedShopVersionCode".toInteger()
            versionName "$libraryVersions.rashedShopVersionName"

            resValue 'string', 'app_name', 'RashedShop'
        }
    }

    //Remove unwanted variants
    android.variantFilter { variant ->
        if (variant.getFlavors().get(0).name.equals('user')
                && (variant.getFlavors().get(1).name.equals('rashedshop'))) {
            variant.setIgnore(true)
        }
        if (variant.getFlavors().get(0).name.equals('admin')
                && (variant.getFlavors().get(1).name.equals('rashedshop'))) {
            variant.setIgnore(true)
        }
        if (variant.getFlavors().get(0).name.equals('driver')
                && (variant.getFlavors().get(1).name.equals('rashedshop'))) {
            variant.setIgnore(true)
        }
    }

    applicationVariants.all { variant ->
        // Define source directory
        def flavors = variant.productFlavors
        def app = flavors[0].name
        def client = flavors[1].name
        System.out.println("app: " + app)
        System.out.println("client: " + client)
        def srcSet = app + client.capitalize()
        System.out.println("srcSet: " + srcSet)
        android.sourceSets."$srcSet".java.srcDirs += "src/$srcSet/java"
        android.sourceSets."$srcSet".res.srcDirs += "src/$srcSet/res"

        // Furnish output and rename the output apk file name
        variant.outputs.all { output ->
            def appName = variant.mergedFlavor.resValues.get("string/app_name")
            def underScore = "_"
            def hyphen = "-"
            def appType = variant.productFlavors[0].name
            def clientType = variant.productFlavors[1].name
            def version = variant.versionName
            def buildType = variant.buildType.name
            System.out.println("appName: " + appName.getValue())
            System.out.println("appType: " + appType)
            System.out.println("clientType: " + clientType)
            System.out.println("version: " + version)
            System.out.println("buildType: " + buildType)
            def releaseApkName = "${appName.getValue()}${underScore}${appType.toUpperCase()}${underScore}${version}${underScore}${buildType.toUpperCase()}.apk"
            output.outputFileName = releaseApkName
            System.out.println("outputFileName: " + outputFileName)

            // set new resValue for final app name
            def finalAppName = ""
            if (appType.equals('user')) {
                finalAppName = appName.getValue()
            } else if (appType.equals('admin')) {
                finalAppName = "${appName.getValue()}${hyphen}${convertToCamelCase(appType)}"
            } else if (appType.equals('driver')) {
                finalAppName = "${appName.getValue()}${hyphen}${convertToCamelCase(appType)}"
            } else {
                finalAppName = appName.getValue()
            }
            System.out.println("finalAppName: " + finalAppName)
            variant.resValue('string', 'app_name', "${finalAppName}")
        }
    }
}

def static String convertToCamelCase(String str) {
    if (str == null) {
        return null
    }

    boolean space = true
    StringBuilder builder = new StringBuilder(str)
    final int len = builder.length()

    for (int i = 0; i < len; ++i) {
        char c = builder.charAt(i)
        if (space) {
            if (!Character.isWhitespace(c)) {
                // Convert to title case and switch out of whitespace mode.
                builder.setCharAt(i, Character.toTitleCase(c))
                space = false
            }
        } else if (Character.isWhitespace(c)) {
            space = true
        } else {
            builder.setCharAt(i, Character.toLowerCase(c))
        }
    }

    return builder.toString()
}

dependencies {
    // Androidx support libraries
    implementation libraries.androidXAppCompat
    implementation libraries.androidXConstraintLayout
    implementation libraries.material
    implementation libraries.androidXMultiDex
    implementation libraries.design

    // Kotlin support
    implementation libraries.kotlinStdlibJdk7
    implementation libraries.androidXCoreKtx

    // Menu
    implementation libraries.flourish
    implementation libraries.powerMenu

    // Calligraphy
    implementation libraries.calligraphy
    implementation libraries.viewpump

    // Sectioned recyclerview
    implementation libraries.sectionedRecyclerView

    // Image loader
    implementation libraries.glide

    // Increment product view
    implementation libraries.incrementProductView

    implementation libraries.colorShadowDrawable

    implementation libraries.androidRandomColor

    implementation('com.meembusoft.recyclerview:mrecyclerview:1.0.01-SNAPSHOT') {
        changing = true
    }

    implementation libraries.gson

    implementation libraries.picasso
    implementation libraries.flexBox

//    implementation 'eu.davidea:flexible-adapter:5.1.0'
//    implementation 'eu.davidea:flexible-adapter-ui:1.0.0'

    // Parceler
    implementation libraries.parcelerApi
    annotationProcessor libraries.parceler

    // Barcode
    implementation libraries.zxingAndroidEmbedded

    // Image picker
    implementation libraries.imagePicker

    // Date time picker
    implementation libraries.materialdDateTimePicker

    // Rating bar
    implementation libraries.materialRatingBar

    // Library projects
    implementation project(":library:localemanager")
    implementation project(":library:recolor")
    implementation project(":library:msvsearch")
    implementation project(":library:retrofitmanager")
    implementation project(":library:ultimatetablayout")
//    implementation project(":library:flexibleadapter")
//    implementation project(":library:flexibleadapterui")
    implementation project(":library:fabulousfilter")
    implementation project(":library:foldablelayout")
    implementation project(":library:shopping")
    implementation project(":library:flowlayout")
    implementation project(":library:expansionpanel")
    implementation project(":library:animationmanager")
    implementation project(":library:realmmanager")
    implementation project(":library:addtocart")
    implementation project(":library:attributionpresenter")
    implementation project(":library:aboutbox")
    implementation project(":library:cookiebar2")
    implementation project(":library:timelineview")
    implementation project(":library:autoimageslider")
}